buildscript {
    ext.kotlin_version = "1.0.3"
    ext.scala_version = "2.11.8"

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.1.10"
}

apply plugin: "idea"
apply plugin: "java"
apply plugin: "kotlin"
apply plugin: "scala"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
}

group "com.demonwav.minecraft-dev"
version pluginVersion

repositories {
    mavenCentral()
}

dependencies {
    compile "org.scala-lang:scala-library:$scala_version"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

intellij {
    // IntelliJ IDEA dependency
    version project.hasProperty("intellijVersion") ? project.getProperty("intellijVersion") : ideaVersion
    // Bundled plugin dependencies
    plugins = ["maven", "gradle", "Groovy", "yaml", "Kotlin", "org.intellij.scala:2016.2.0"]
    pluginName "Minecraft Development"
    updateSinceUntilBuild false

    downloadSources Boolean.valueOf(downloadIdeaSources)
    sandboxDirectory project.rootDir.canonicalPath + "/.sandbox"
}

idea {
    project {
        jdkName = 1.8
        languageLevel = 1.8
    }

    module {
        excludeDirs += file(".sandbox");
    }
}

task initProp << {
    def baseProp = file("src/main/resources/messages.MinecraftDevelopment_en.properties")
    def baseEnglishProp = file("src/main/resources/messages.MinecraftDevelopment.properties")

    def comment =
            "# Do not manually edit this file\n" +
            "# This file is automatically copied from messages.MinecraftDevelopment_en_US.properties at build time\n"

    def baseUsEnglishProp = file("src/main/resources/messages.MinecraftDevelopment_en_US.properties")

    baseProp.write comment + baseUsEnglishProp.text
    baseEnglishProp.write comment + baseUsEnglishProp.text
}

afterEvaluate {
    buildPlugin.dependsOn initProp
    runIdea.dependsOn initProp
}
